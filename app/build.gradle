plugins {
    id "com.android.application"
    id "kotlin-android"
}

android {
    namespace "com.mrboomdev.awery"
    compileSdk 34

    defaultConfig {
        applicationId "com.mrboomdev.awery"
        targetSdk 34
        versionName "1.0.4"

        /* We want to make sure that all build versions across the planet will be in sync */
        var timezone = TimeZone.getTimeZone("UTC")
        var calendarCurrent = Calendar.getInstance(timezone)

        /* Just a random date to begin with */
        var calendarBegin = Calendar.getInstance(timezone)
        calendarBegin.set(2024, 2, 4)

        /* Only one version can be published per hour or else horrible things will happen */
        var newVersionCodeTimeout = 60 * 60 * 1000
        versionCode (((calendarCurrent.timeInMillis - calendarBegin.timeInMillis) / newVersionCodeTimeout) as int)

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    androidResources {
        generateLocaleConfig true
    }

    sourceSets {
        main.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }

        release {
            versionNameSuffix "-release"
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions = [ "type", "sdk" ]

    productFlavors {
        beta {
            dimension "type"
            versionNameSuffix "-beta"
            applicationIdSuffix ".beta"
            buildConfigField("boolean", "IS_BETA", "true")
            isDefault true
        }

        prod {
            dimension "type"
            versionNameSuffix "-prod"
            buildConfigField("boolean", "IS_BETA", "false")
        }

        modern {
            versionNameSuffix "-modern"
            dimension "sdk"
            buildConfigField("boolean", "IS_LEGACY", "false")
            isDefault true
            minSdk 25
        }

        legacy {
            versionNameSuffix "-legacy"
            dimension "sdk"
            buildConfigField("boolean", "IS_LEGACY", "true")
            minSdk 23
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs = ["-Xcontext-receivers", "-Xmulti-platform"]
    }
}

dependencies {
    // AwerySdk
    implementation "com.github.MrBoomDeveloper:AwerySdk:653d1e0396"

    // Debugging
    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.14"

    // Compatibility for new methods in old Api
    implementation "androidx.core:core-ktx:1.13.1"
    implementation "androidx.work:work-runtime-ktx:2.9.0"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.browser:browser:1.8.0"
    implementation "androidx.webkit:webkit:1.11.0"
    implementation "androidx.fragment:fragment-ktx:1.7.0"
    implementation "androidx.preference:preference-ktx:1.2.1"

    // Exoplayer
    ext.exo_version = "1.3.1"
    implementation "androidx.media3:media3-exoplayer:$exo_version"
    implementation "androidx.media3:media3-ui:$exo_version"
    implementation "androidx.media3:media3-exoplayer-hls:$exo_version"
    implementation "androidx.media3:media3-exoplayer-dash:$exo_version"
    implementation "androidx.media3:media3-datasource-okhttp:$exo_version"
    implementation "androidx.media3:media3-session:$exo_version"

    // Database
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // UI
    implementation "androidx.recyclerview:recyclerview:1.3.2"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "com.google.android.material:material:1.11.0"
    implementation "nl.joery.animatedbottombar:library:1.1.0"
    implementation "io.noties.markwon:core:4.6.2"
    implementation "com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0"
    implementation "com.github.VipulOG:ebook-reader:0.1.6"
    implementation "androidx.paging:paging-runtime-ktx:3.2.1"
    implementation "com.github.skydoves:colorpickerview:2.3.0"
    implementation "androidx.core:core-splashscreen:1.0.1"
    implementation "com.github.skydoves:balloon:1.6.4"

    // Utils
    implementation "me.xdrop:fuzzywuzzy:1.4.0"
    implementation "com.squareup.moshi:moshi:1.15.0"
    implementation "org.mozilla:rhino:1.7.14"
    implementation "com.iqiyi.xcrash:xcrash-android-lib:3.1.0"
    implementation "net.sourceforge.streamsupport:android-retrostreams:1.7.4"

    // Networking
    ext.glide_version = "4.16.0"
    ext.okhttp_version = "5.0.0-alpha.12"
    api "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:annotations:glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:4.14.2"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp-dnsoverhttps:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp-brotli:4.12.0"

    // Aniyomi
    ext.kotlin_serialization_verison = "1.6.2"
    implementation "app.cash.quickjs:quickjs-android:0.9.2"
    implementation "io.reactivex:rxjava:1.3.8"
    implementation "io.reactivex:rxandroid:1.2.1"
    implementation "com.github.inorichi.injekt:injekt-core:65b0440"
    implementation "org.jsoup:jsoup:1.15.4"
    implementation "com.github.gpanther:java-nat-sort:natural-comparator-1.1"
    implementation "com.github.tachiyomiorg:unifile:17bec43"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlin_serialization_verison"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json-okio:$kotlin_serialization_verison"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-protobuf:$kotlin_serialization_verison"
}